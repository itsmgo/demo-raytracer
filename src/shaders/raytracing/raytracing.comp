#version 430 core

layout(local_size_x = 20, local_size_y = 20, local_size_z = 1) in;
layout(rgba32f, binding = 0) uniform image2D imgOutput;
layout(rgba32f, binding = 1) uniform image2D imgOutputHalf;

struct BVH_Node {
  vec4 childrenId_ObjectInfo, pMin, pMax;
};

struct BVH_Object {
  vec4 objInfo;
};

struct BVH_Triangle {
  vec4 v1_pos_Nx, v1_nor_Txcoords, v2_pos_Nx, v2_nor_Txcoords, v3_pos_Nx,
      v3_nor_Txcoords;
};

layout(std430, binding = 0) readonly buffer BVH_Nodes { BVH_Node nodes[]; }
BVHTree;

layout(std430, binding = 1) readonly buffer BVH_Objects {
  BVH_Object objects[];
}
BVHObjects;

layout(std430, binding = 2) readonly buffer BVH_Triangles {
  BVH_Triangle triangles[];
}
BVHTriangles;

struct Camera {
  vec3 position;
  vec3 front;
  vec3 right;
  vec3 up;
  float zoom;
};

struct Ray {
  vec3 origin;
  vec3 direction;
};

struct Hit {
  vec3 position;
  vec3 normal;
  float t;
  bool frontFace;
};

uniform Camera camera;
uniform samplerBuffer trianglesBuffer;
uniform int currentSample;
uniform int numTriangles;
uniform int width;
uniform int height;

float FOV = radians(camera.zoom * 0.5);
float focusDist = length(camera.position);
float aperture = 0.030;

const float MAX_DISTANCE = 999999;
const float MIN_DISTANCE = 0.00001;
const int MAX_BOUNCES = 8;
const float GAMMA = 2.0;

int SEED = 1;

// A single iteration of Bob Jenkins' One-At-A-Time hashing algorithm.
uint hash(uint x) {
  x += (x << 10u);
  x ^= (x >> 6u);
  x += (x << 3u);
  x ^= (x >> 11u);
  x += (x << 15u);
  return x;
}

uint hash(uvec4 v) {
  SEED++;
  return hash(v.x ^ hash(v.y) ^ hash(v.z) ^ hash(SEED));
}

// Construct a float with half-open range [0:1] using low 23 bits.
// All zeroes yields 0.0, all ones yields the next smallest representable value
// below 1.0.
float floatConstruct(uint m) {
  const uint ieeeMantissa = 0x007FFFFFu; // binary32 mantissa bitmask
  const uint ieeeOne = 0x3F800000u;      // 1.0 in IEEE binary32

  m &= ieeeMantissa; // Keep only mantissa bits (fractional part)
  m |= ieeeOne;      // Add fractional part to 1.0

  float f = uintBitsToFloat(m); // Range [1:2]
  return f - 1.0;               // Range [0:1]
}

// Pseudo-random value in half-open range [0:1].
float random() {
  SEED++;
  return floatConstruct(hash(floatBitsToUint(vec4(
      gl_GlobalInvocationID.x, gl_GlobalInvocationID.y, currentSample, SEED))));
}

vec3 randomUnitInSphere() {
  while (true) {
    vec3 point = vec3(random(), random(), random()) * 2 - vec3(1, 1, 1);
    if (pow(length(point), 2) < 1)
      return normalize(point);
  }
}

vec2 randomInDisk() {
  while (true) {
    vec2 point = vec2(random(), random()) * 2 - vec2(1, 1);
    if (pow(length(point), 2) < 1)
      return point;
  }
}

Hit hitSphere(vec3 center, float radius, Ray ray) {
  vec3 oc = ray.origin - center;
  float a = pow(length(ray.direction), 2);
  float half_b = dot(oc, ray.direction);
  float c = pow(length(oc), 2) - radius * radius;
  float discriminant = half_b * half_b - a * c;

  Hit hit;
  if (discriminant < 0) {
    hit.t = -1.0;
    return hit;
  }
  hit.t = (-half_b - sqrt(discriminant)) / a;
  hit.position = ray.origin + ray.direction * hit.t;
  hit.normal = normalize(hit.position - center);
  return hit;
}

Hit rayTriangleIntersect(Ray ray,                   // ray
                         vec3 p1, vec3 p2, vec3 p3, // vertices positions
                         vec3 n1, vec3 n2, vec3 n3) // vertices normals
{
  Hit hit;
  vec3 edge1 = p2 - p1;
  vec3 edge2 = p3 - p1;
  // compute the plane's normal
  vec3 h = cross(ray.direction, edge2);
  float a = dot(edge1, h);
  if (a > -MIN_DISTANCE && a < MIN_DISTANCE) {
    hit.t = -1;
    return hit;
  }
  float f = 1.0 / a;
  vec3 s = ray.origin - p1;
  float u = f * dot(s, h);
  if (u < 0.0 || u > 1.0) {
    hit.t = -1;
    return hit;
  }
  vec3 q = cross(s, edge1);
  float v = f * dot(ray.direction, q);
  if (v < 0.0 || u + v > 1.0) {
    hit.t = -1;
    return hit;
  }
  hit.t = f * dot(edge2, q);
  if (hit.t > MIN_DISTANCE) {
    hit.position = ray.origin + ray.direction * hit.t;
    hit.normal = (1 - u - v) * n1 + u * n2 + v * n3; // interpolate normals
  }
  return hit;
}

vec3 getBackgroundColor(Ray ray) {
  float t = 0.5 * (ray.direction.y + 1.0);
  return (1.0 - t) * vec3(1.0, 1.0, 1.0) + t * vec3(0.5, 0.7, 1.0);
}

Hit getObjectClosestHit(Ray ray, int offsetTriangles, int countTriangles) {
  Hit closestHit;
  closestHit.t = MAX_DISTANCE;
  for (int i = offsetTriangles; i < offsetTriangles + countTriangles; i++) {
    // vertex 1
    vec3 p1 = BVHTriangles.triangles[i].v1_pos_Nx.xyz;
    vec3 n1 = vec3(BVHTriangles.triangles[i].v1_pos_Nx.a,
                   BVHTriangles.triangles[i].v1_nor_Txcoords.xy);
    // vec2 t1 = vec2(BVHTriangles.triangles[i].pos_Nx.z,
    //                BVHTriangles.triangles[i].pos_Nx.a);

    // vertex 2
    vec3 p2 = BVHTriangles.triangles[i].v2_pos_Nx.xyz;
    vec3 n2 = vec3(BVHTriangles.triangles[i].v2_pos_Nx.a,
                   BVHTriangles.triangles[i].v2_nor_Txcoords.xy);
    // vec2 t2 = vec2(BVHTriangles.triangles[i].pos_Nx.z,
    //                BVHTriangles.triangles[i].pos_Nx.a);

    // vertex 3
    vec3 p3 = BVHTriangles.triangles[i].v3_pos_Nx.xyz;
    vec3 n3 = vec3(BVHTriangles.triangles[i].v3_pos_Nx.a,
                   BVHTriangles.triangles[i].v3_nor_Txcoords.xy);
    // vec2 t3 = vec2(BVHTriangles.triangles[i].pos_Nx.z,
    //                BVHTriangles.triangles[i].pos_Nx.a);

    Hit hit = rayTriangleIntersect(ray, p1, p2, p3, n1, n2, n3);
    // Hit hit = hitSphere(p1, length(p2 - p1), ray);
    if (hit.t < MIN_DISTANCE || hit.t > closestHit.t) {
      continue;
    }
    closestHit = hit;
  }
  if (closestHit.t == MAX_DISTANCE) {
    closestHit.t = -1.0;
    return closestHit;
  }
  return closestHit;
}

Hit getMultipleObjectsClosestHit(Ray ray, int objOffset, int objCount) {
  Hit closestHit;
  Hit hit;
  closestHit.t = MAX_DISTANCE;
  for (int i = objOffset; i < objOffset + objCount; i++) {
    hit = getObjectClosestHit(ray, // ray
                              int(BVHObjects.objects[i].objInfo.x),
                              int(BVHObjects.objects[i].objInfo.y));
    if (hit.t < MIN_DISTANCE || hit.t > closestHit.t) {
      continue;
    }
    closestHit = hit;
  }
  if (closestHit.t == MAX_DISTANCE) {
    closestHit.t = -1.0;
  }
  return closestHit;
}

bool rayBoxIntersect(vec3 origin, vec3 dirfrac, vec3 pMin, vec3 pMax) {
  float t1 = (pMin.x - origin.x) * dirfrac.x;
  float t2 = (pMax.x - origin.x) * dirfrac.x;
  float t3 = (pMin.y - origin.y) * dirfrac.y;
  float t4 = (pMax.y - origin.y) * dirfrac.y;
  float t5 = (pMin.z - origin.z) * dirfrac.z;
  float t6 = (pMax.z - origin.z) * dirfrac.z;

  float tmin = max(max(min(t1, t2), min(t3, t4)), min(t5, t6));
  float tmax = min(min(max(t1, t2), max(t3, t4)), max(t5, t6));

  return (tmax >= 0 && tmin <= tmax);
}

Hit traverseBVH(Ray ray) {
  vec3 color = vec3(0);
  Hit closestHit;
  Hit hit;
  closestHit.t = MAX_DISTANCE;
  vec3 dirfrac = 1.0 / ray.direction;
  // Follow ray through BVH nodes to find primitive intersections
  int toVisitOffset = 0;
  int currentNodeIndex = 0;
  int nodesToVisit[64];
  while (true) {
    BVH_Node node = BVHTree.nodes[currentNodeIndex];
    // Check ray against BVH node
    if (rayBoxIntersect(ray.origin, dirfrac, node.pMin.xyz, node.pMax.xyz)) {
      if (int(node.childrenId_ObjectInfo.x) < 0) { // LEAF
        // Intersect ray with primitives in leaf BVH node
        hit = getMultipleObjectsClosestHit(ray, // ray
                                            int(node.childrenId_ObjectInfo.z),
                                            int(node.childrenId_ObjectInfo.a));
        if (hit.t < closestHit.t && hit.t > MIN_DISTANCE) {
          closestHit = hit;
        }
        if (toVisitOffset == 0)
          break;
        currentNodeIndex = nodesToVisit[--toVisitOffset];
        continue;
      }
      // NODE
      // Put second BVH node on nodesToVisit stack, advance to near node
      currentNodeIndex = int(node.childrenId_ObjectInfo.y);
      nodesToVisit[toVisitOffset++] = int(node.childrenId_ObjectInfo.x);
      continue;
    }
    if (toVisitOffset == 0) {
      break;
    }
    currentNodeIndex = nodesToVisit[--toVisitOffset];
  }
  if (closestHit.t == MAX_DISTANCE) {
    closestHit.t = -1.0;
  }
  return closestHit;
}

vec3 getRayColor(Ray ray) {
  vec3 color = vec3(1.0, 1.0, 1.0);
  Ray currentRay = ray;

  for (int bounce = 0; bounce < MAX_BOUNCES; bounce++) {
    Hit hit = traverseBVH(currentRay);

    if (hit.t < MIN_DISTANCE) {
      color *= getBackgroundColor(currentRay);
      return color;
    }

    currentRay.origin = hit.position;
    currentRay.direction = normalize(hit.normal + randomUnitInSphere());
    color *= 0.5;
  }
  return color;
}

Ray getTexelRay(vec2 texelCoord) {
  float x = texelCoord.x / width * 2 - 1;
  float y = texelCoord.y / height * 2 - 1;

  float aspectRatio = float(width) / height;

  vec2 randomDisk = aperture * 0.5 * randomInDisk();
  vec3 offset = camera.right * randomDisk.x + camera.up * randomDisk.y;

  vec3 frontal = camera.front * focusDist;
  vec3 vertical = camera.up * tan(FOV) * focusDist;
  vec3 horizontal = camera.right * tan(FOV) * aspectRatio * focusDist;

  vec3 P = camera.position + frontal + horizontal * x + vertical * y;

  Ray ray;
  ray.origin = camera.position + offset;
  ray.direction = normalize(P - camera.position - offset);
  return ray;
}

void main() {
  ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
  if (texelCoord.x > width || texelCoord.y > height)
    return;

  vec2 coord =
      vec2(float(texelCoord.x) + random(), float(texelCoord.y) + random());
  Ray ray = getTexelRay(coord);

  vec3 color = getRayColor(ray);

  if (currentSample == 1) {
    imageStore(
        imgOutputHalf, texelCoord,
        vec4(pow(color, vec3(1.0 / GAMMA, 1.0 / GAMMA, 1.0 / GAMMA)), 1.0));
    return;
  }
  if (currentSample == 2) {
    imageStore(
        imgOutput, texelCoord,
        vec4(pow(color, vec3(1.0 / GAMMA, 1.0 / GAMMA, 1.0 / GAMMA)), 1.0));
    return;
  }

  // Divide the color by the number of samples.
  float scale = 1.0 / currentSample;
  color *= scale;

  vec4 accColor = imageLoad(imgOutput, texelCoord);
  accColor = pow(accColor, vec4(GAMMA, GAMMA, GAMMA, 1.0));
  accColor *= float(currentSample - 1) / float(currentSample);
  accColor += vec4(color, 1.0);
  accColor = pow(accColor, vec4(1.0 / GAMMA, 1.0 / GAMMA, 1.0 / GAMMA, 1.0));
  imageStore(imgOutput, texelCoord, accColor);
}
